grammar FOOL;
 
@lexer::members {
public int lexicalErrors=0;
}
   
/*------------------------------------------------------------------
 * PARSER RULES
 *------------------------------------------------------------------*/
  
prog  : progbody EOF ;
     
progbody : LET dec+ IN exp SEMIC  #letInProg
         | exp SEMIC              #noDecProg
         ;
  
dec : VAR ID COLON type ASS exp SEMIC  #vardec
    | FUN ID COLON type LPAR (ID COLON type (COMMA ID COLON type)* )? RPAR 
        	(LET dec+ IN)? exp SEMIC   #fundec
    ;

// LE PRODUZIONI HANNO PRIORITÀ IN BASE ALL'ORDINE DI DICHIARAZIONE (ES: TIMES HA PRIORIÀ RISPETTO A PLUS).
// NEL COMPORTAMENTO DI DEFAULT VIENE USATA L'ASSOCIAZIONE A SINISTRA.
// # STABILISCE IL NOME DELLA PRODUZIONE.
exp     : exp TIMES exp #times
        | exp PLUS  exp #plus
        | exp EQ  exp   #eq 
        | LPAR exp RPAR #pars
    	| MINUS? NUM #integer
	    | TRUE #true     
	    | FALSE #false
	    | IF exp THEN CLPAR exp CRPAR ELSE CLPAR exp CRPAR  #if   
	    | PRINT LPAR exp RPAR #print
	    | ID #id
	    | ID LPAR (exp (COMMA exp)* )? RPAR #call
        ; 
             
type    : INT #intType
        | BOOL #boolType
 	    ;  
 	  		  
/*------------------------------------------------------------------
 * LEXER RULES
 * PRECEDENZA IN BASE ALL'ORDINE DI DICHIARAZIONE (PLUS È IL PIÙ RILEVANTE, MENTRE ERR È IL MENO RILEVANTE)
 *------------------------------------------------------------------*/

PLUS  	: '+' ;
MINUS	: '-' ; 
TIMES   : '*' ;
LPAR	: '(' ;
RPAR	: ')' ;
CLPAR	: '{' ;
CRPAR	: '}' ;
SEMIC 	: ';' ;
COLON   : ':' ; 
COMMA	: ',' ;
EQ	    : '==' ;	
ASS	    : '=' ;
TRUE	: 'true' ;
FALSE	: 'false' ;
IF	    : 'if' ;
THEN	: 'then';
ELSE	: 'else' ;
PRINT	: 'print' ;
LET     : 'let' ;	
IN      : 'in' ;	
VAR     : 'var' ;
FUN	    : 'fun' ;	  
INT	    : 'int' ;
BOOL	: 'bool' ;
NUM     : '0' | ('1'..'9')('0'..'9')* ; 

ID  	: ('a'..'z'|'A'..'Z')('a'..'z' | 'A'..'Z' | '0'..'9')* ;

// CON channel(HIDDEN) IL TOKEN VIENE NASCOSTO AL PARSER.
WHITESP  : ( '\t' | ' ' | '\r' | '\n' )+    -> channel(HIDDEN) ;

// IL PUNTO INDICA TUTTI I CARATTERI DELL'ALFABETO.
// *? È LA STELLA NON GREEDY, APPENA PUÒ ESCE.
// SENZA ?, SE INSERSCO PIÙ COMMENTI, VIENE CONSIDERATO IL /* DEL PRIMO E IL */ DELL'ULTIMO,
// QUINDI VENGONO COMMENTATE ANCHE LA PARTI DI CODICE IN MEZZO.
COMMENT : '/*' .*? '*/' -> channel(HIDDEN) ;
 
ERR   	 : . { System.out.println("Invalid char "+getText()+" at line "+getLine()); lexicalErrors++; } -> channel(HIDDEN); 


